buildscript {
    ext.repos = {
        mavenCentral() {
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }
    }
    ext.aopVersion = '6.1.6'
    repositories repos
    dependencies {
        classpath "com.axelor:axelor-gradle:${aopVersion}"
        classpath "com.adarshr:gradle-test-logger-plugin:2.+"
    }
}

repositories repos

apply from: "version.gradle"
apply plugin: "com.axelor.app"
apply plugin: 'com.adarshr.test-logger'

try {
    tasks.named("spotlessCheck")
} catch (UnknownTaskException ignored) {
    apply from: './gradle/style.gradle'
}


testlogger {
    theme 'mocha'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.axelor.addons"
description "Axelor Utils Module"

axelor {
    title "Axelor Utils"
    description "Axelor Utils Module"
}

dependencies {
    implementation 'commons-net:commons-net:2.0'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.commons:commons-lang3:3.2'
    implementation 'org.apache.pdfbox:pdfbox:2.0.9'
    implementation 'org.apache.commons:commons-text:1.9'

    // JSch
    implementation 'com.jcraft:jsch:0.1.55'

    compileOnly 'javax.xml.ws:jaxws-api:2.3.1'
    compileOnly 'javax.xml.bind:jaxb-api:2.2.2'

    implementation 'com.sun.xml.bind:jaxb-impl:2.2.2'
    api 'com.opencsv:opencsv:4.6'
    implementation 'javax.xml.ws:jaxws-api:2.3.1'
    implementation 'javax.jws:javax.jws-api:1.1'
    implementation 'org.reflections:reflections:0.10.2'

    testImplementation 'org.junit.vintage:junit-vintage-engine'
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
    mavenUsername = project.findProperty("addonsMavenUsername")
    mavenPassword = project.findProperty("addonsMavenPassword")
}

publishing {
    repositories {
        maven {
            def repoPath = project.hasProperty("finalRelease")
                    ? "maven-releases"
                    : "maven-snapshots"
            name = "maven"
            url = "https://repository.axelor.com/nexus/repository/" + repoPath
            credentials {
                username = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}

/*
* Jacoco
*/

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
    // report is always generated after tests run
    finalizedBy jacocoTestReport
    maxHeapSize = '1G'
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test
}
