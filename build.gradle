import com.axelor.gradle.support.ChangelogSupport

buildscript {
    ext.repos = {
        mavenCentral() {
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }
    }
    ext.aopVersion = '7.0.+'
    repositories repos
    buildscript.repositories repos
    dependencies {
        classpath "com.axelor:axelor-gradle:${aopVersion}"
        classpath "com.adarshr:gradle-test-logger-plugin:2.+"
    }
}

repositories repos
buildscript.repositories repos

apply from: "version.gradle"
apply plugin: "com.axelor.app"
apply plugin: 'com.adarshr.test-logger'
apply plugin: ChangelogSupport

try {
    tasks.named("spotlessCheck")
} catch (UnknownTaskException ignored) {
    apply from: './gradle/style.gradle'
}


testlogger {
    theme 'mocha'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.axelor.addons"
description "Axelor Utils Module"

axelor {
    title "Axelor Utils"
    description "Axelor Utils Module"
}

dependencies {
    implementation 'commons-net:commons-net:3.10.0'
    implementation 'commons-io:commons-io:2.15.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.apache.pdfbox:pdfbox:3.0.0'
    implementation 'org.apache.commons:commons-text:1.11.0'

    // JSch
    implementation 'com.jcraft:jsch:0.1.55'

    implementation 'javax.xml.ws:jaxws-api:2.3.1'

    implementation 'com.sun.xml.bind:jaxb-impl:4.0.4'
    implementation 'org.apache.commons:commons-csv:1.10.0'

    implementation 'javax.jws:javax.jws-api:1.1'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.3'

    implementation "com.axelor:axelor-test:${aopVersion}"

    testImplementation 'org.junit.vintage:junit-vintage-engine'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

changelog {
    version = baseVersion
    output.set(file("CHANGELOG.md"))
    inputPath.set(file("changelogs/unreleased"))
    types.set(["Feature", "Change", "Deprecate", "Remove", "Fix", "Security"])
    header.set("${version.get()} (${new Date().format("yyyy-MM-dd")})")
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
    mavenUsername = project.findProperty("addonsMavenUsername")
    mavenPassword = project.findProperty("addonsMavenPassword")
}

publishing {
    repositories {
        maven {
            def repoPath = project.hasProperty("finalRelease")
                    ? "maven-releases"
                    : "maven-snapshots"
            name = "maven"
            url = "https://repository.axelor.com/nexus/repository/" + repoPath
            credentials {
                username = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}

/*
* Jacoco
*/

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
    // report is always generated after tests run
    finalizedBy jacocoTestReport
    maxHeapSize = '1G'
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test
}
